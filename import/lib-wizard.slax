/*
 * $Id$
 *
 * Copyright (c) 2006-2011, Juniper Networks, Inc.
 * All rights reserved.
 * This SOFTWARE is licensed under the LICENSE provided in the
 * ../Copyright file. By downloading, installing, copying, or otherwise
 * using the SOFTWARE, you agree to be bound by the terms of that
 * LICENSE.
 */

version 1.0;

ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns wizard extension = "http://xml.juniper.net/wizard"; /* For use by others */
ns wiz extension = "http://xml.juniper.net/wiz";       /* For use by us */
ns func extension = "http://exslt.org/functions";
ns dyn extension = "http://exslt.org/dynamic";
ns exslt = "http://exslt.org/common";
ns slax = "http://code.google.com/p/libslax/slax";

import "../import/lib-phil.slax";

var $lib-wizard-version = "20110207";

var $value-indent = 40; /* Where to break title/value boundary */
var $new-indent = "   ";

var $typedefs := {
    <typedef name="dnsname"> {
	<match> {
	    <regex> "^[a-zA-Z0-9._-]{1,255}$";
	    <message> "must contain 1-255 letters, numbers and characters";
	}
    }
    <typedef name="ip-address"> {
	<match> {
	    <regex> "^[0-9]{1,3}\\.[0-9]{1,3}\\.[0-9]{1,3}\\.[0-9]{1,3}$";
	    <message> "must be a valid IPv4 address";
	}
    }
    <typedef name="ip-prefix"> {
	<match> {
	    <regex> "^[0-9]{1,3}\\.[0-9]{1,3}\\.[0-9]{1,3}\\.[0-9]{1,3}/[0-9]{1,2}$";
	    <message> "must be a valid IPv4 prefix";
	}
    }
}

template wizard-builder ($config, $guide, $title) {
    call wiz:build-config($config, $guide, $title, $last = /null);
}

template wiz:build-config ($config, $guide, $last, $title) {
    var $new := {
	call build($config, $last, $guide);
    }

    if (0) {
	call dump($name = "new", $out = $new);
    }

    expr wiz:message("\nconfiguration:");
    call display-config($guide, $config = $new);

    var $response = {
	call wiz:prompt($prompt = "is this configuration accurate?",
		    $default = "no", $type = "yes/no");
    }

    if ($response == "yes") {
	/*
	 * we can now return the built configuration to our caller!!
	 */
	copy-of $new;

    } else {
	var $response2 = {
	    call wiz:prompt($prompt = "would you like to change it, or exit?",
			$default = "change", $type = "change,exit");
	}
	if ($response2 == "exit") {
	    expr wiz:message("exit from ", $title);
	    /* return nothing */
	} else {
	    expr wiz:message("please correct configuration data as needed");
	    call wiz:build-config($config, $guide, $last = $new, $title);
	}
    }
}

template promptyesno ($prompt) {
    call wiz:promptyesno($prompt);
}

template wiz:promptyesno ($prompt) {
    var $fullprompt = $prompt _ " [yes/no]: ";
    var $response = wiz:get-input($fullprompt);
    if ($response == "yes" || $response == "no") {
	expr $response;
    } else {
	expr wiz:message("please answer 'yes' or 'no'");
	call wiz:promptyesno($prompt);
    }
}

/*
 * display the banner message to the user
 */
template wiz:banner-message ($banner-message) {
    for-each ($banner-message/line) {
	expr wiz:message(.);
    }
}

template banner-message ($banner-message) {
    call wiz:banner-message($banner-message);
}

/*
 * prompt for the value, with bells and whistles.
 * "." == empty, since there's a pr for empty lines.
 * if the input is empty and there was a previous value, use it.
 */
template wiz:prompt ($prompt, $previous, $node = /null,
		 $default = $node/@default, $mandatory = $node/@mandatory,
		 $type = $node/@type, $range = $node/@range,
		 $help = $node/help) {
    var $fullprompt = {
	expr $prompt;
	if ($type == "secret") {
	    /* nothing */
	} else if (string-length($previous) > 0) {
	    expr " (was: " _ $previous _ ")";
	} else if ($default) {
	    expr " (default: " _ $default _ ")";
	}
	if ($type) {
	    if ($type == "empty") {
		expr " [yes/no]";
	    } else {
		expr " [" _ $type _ "]";
	    }
	}
	if ($mandatory) {
	    expr " [mandatory]";
	}
	expr ": ";
    }

    /*
     * for 'secret' data, we use wiz:get-secret to avoid displaying
     * the input string.
     */
    if ($type == "secret" && function-available('wiz:get-secret')) {
	var $response = wiz:get-secret($fullprompt);
	if ($response == ".") {
	    expr $previous;
	} else {
	    var $prompt2 = $prompt _ " (re-enter to confirm) :";
	    var $response2 = wiz:get-secret($prompt2);
	    if ($response2 == $response) {
		expr $response;
	    } else {
		expr wiz:message(">>> invalid response: ",
				"values were not identical");
		call wiz:prompt($prompt, $previous, $node, $help,
			    $default, $mandatory, $type);
	    }
	}
    } else {

	var $response = wiz:get-input($fullprompt);
	if ($response == "?") {
	    if ($help) {
		for-each ($help/node()) {
		    expr wiz:message(.);
		}
	    } else {
	        expr wiz:message(">>> no help message is available");
	    }
	    call wiz:prompt($prompt, $previous, $node, $help,
			$default, $mandatory, $type);
	    
	} else if ($response == "") {
	    /*
             * Bad news: if one hits "^C", wiz:get-input returns
             * an empty string (and CLI starts discarding
             * all output).  This means we have to detect
             * empty lines and abort.
	    call wiz:abort($message = "Invalid blank line (sorry; use '.')");
             */

	    /* Return the previous value */
	    expr $previous;

	} else if ($response != ".") {
	    if (not($type)) {
		expr $response;
	    } else if ($type == "yes/no" || $type == "empty") {
		if ($response == "yes" || $response == "no") {
		    expr $response;
		} else {
		    expr wiz:message("Please answer 'yes' or 'no'");
		    call wiz:prompt($prompt, $previous, $node, $help,
				$default, $mandatory, $type);
		}
	    } else if ($type == "integer") {
		if ($range) {
		    var $re = wiz:regex("([0-9]+)-([0-9]+)", $range);
		    if ($re[1] && ($response < $re[2] || $response > $re[3])) {
			expr wiz:message(">>> invalid response: ",
					"must be within range of ",
					$re[2], "..", $re[3]);
			call wiz:prompt($prompt, $previous, $node, $help,
				    $default, $mandatory, $type, $range);
		    } else {
			expr $response;
		    }
		} else {
		    expr $response;
		}

	    } else {
		var $td = $typedefs/node()[@name == $type];
		if ($td && $td/match/regex) {
		    var $re = wiz:regex($td/match/regex, $response);
		    if ($re[1]) {
			expr $response;
		    } else {
			expr wiz:message(">>> invalid response: ",
					$td/match/message);
			call wiz:prompt($prompt, $previous, $node, $help,
				    $default, $mandatory, $type);
		    }
		} else {
		    expr $response;
		}
	    }
	} else if ($previous) {
	    expr $previous;
	}
    }
}

template prompt ($prompt, $previous, $node = /null,
		 $default = $node/@default, $mandatory = $node/@mandatory,
		 $type = $node/@type, $range = $node/@range,
		 $help = $node/help) {
    call prompt ($prompt, $previous, $node, $default, $mandatory,
		 $type, $range, $help);
}

template wiz:evaluate ($expression) {
    /*
     * This is the odd case of "The RTF That Wouldn't Go Away".
     * dyn:evaluate calls a function that returns a template,
     * but that template is an RTF.  The normal ":=" trick won't
     * fly, so we need to do it the long way.  One more pin goes
     * into my voodoo doll for the inventor of RTF.
     */
    var $rtf = dyn:evaluate(guide-builder);
    copy-of $rtf;
}

template wiz:build ($config, $last, $guide, $indent = $new-indent) {
    if (guide-builder) {
	var $new-guide := { call wiz:evaluate($expression = guide-builder); }

	call wiz:build-internal($config, $last, $guide = $new-guide, $indent);

    } else {
	call wiz:build-internal($config, $last, $guide, $indent);
    }
}

template build ($config, $last, $guide, $indent = $new-indent) {
    call wiz:build ($config, $last, $guide, $indent);
}

/*
 * This is the core of the config builder.  Guided by $guide, we prompt
 * the user for information and assemble it into configuration.
 */
template wiz:build-internal ($config, $last, $guide, $indent) {
    for-each ($guide/node()) {
	var $path = @path;
	var $this-config = $config/node()[name() == $path];
	var $this-last = $last/node()[name() == $path];

	if (name() == "container") {
	    var $enable = {
		if (@ask) {
		    var $prompt = $indent _ "Configure " _ @title _ "?";
		    var $previous = {
			if ($this-last) {
			    expr "yes";
			} else if ($last) {
			    expr "no";
			} else if ($this-config) {
			    expr "yes";
			}
		    }
		    call wiz:prompt($prompt, $previous,
				$default = @default, $type = "yes/no");
		    
		} else {
		    expr "yes";
		}
	    }

	    if ($enable == "yes") {

		var $ind = {
		    if (@title) {
			if (not(@ask)) {
			    expr wiz:message($indent,
					    "Entering data for ", @title, ":");
			}
			expr $indent _ $new-indent;
		    } else {
			expr $indent;
		    }
		}

		var $new := {
		    call wiz:build($config = $this-config,
			       $last = $this-last,
			       $guide = ., $indent = $ind);
		}
		if ($new/node()) {
		    <xsl:element name=$path> {
			copy-of $new;
		    }
		}
	    }

	} else if (name() == "option") {
	    var $prompt = $indent _ "Configure " _ @title _ "?";
	    var $previous = {
		if ($this-last) {
		    expr "yes";
		} else if ($last) {
		    expr "no";
		} else if ($this-config) {
		    expr "yes";
		}
	    }
	    var $value = {
		call wiz:prompt($prompt, $previous,
			    $default = @default, $type = "yes/no");
	    }
	    if ($value == "yes") {
		var $new := {
		    call wiz:build($config = $this-config,
			       $last = $this-last,
			       $guide = ., $indent = $indent _ $new-indent);
		}
		if ($new/node()) {
		    <xsl:element name=$path> {
			copy-of $new;
		    }
		}
	    }

	} else if (name() == "list") {
	    var $list := {
	        if ($this-last) {
		    copy-of $this-last;
		} else if ($this-config) {
		    copy-of $this-config;
		}
	    }

	    if (count($list/node()) <= 0) {
		var $prompt = $indent _ "Configure " _ @title _ "?";
		var $value = {
		    call wiz:prompt($prompt, $default = "no", $type = "yes/no");
		}
		if ($value == "yes") {
		    call wiz:manage-list($list, $guide = .,
				$indent = $indent _ $new-indent,
				$op = "first-add");
		}
	    } else {
		call wiz:manage-list($list, $guide = .,
			    $indent, $op = "list");
	    }

	} else if (name() == "choice") {
	    call wiz:manage-choice($guide = ., $config, $last, $indent);

	} else if (name() == "leaf") {
	    var $prompt = $indent _ "Enter " _ @title;
	    var $previous = {
	        var $prev = wiz:first-of($this-last, $this-config);
		if (@type == "empty") {
		    if ($prev) {
			expr "yes";
		    }
		} else {
		    expr $prev;
		}
	    }

	    var $value = {
		call wiz:prompt($prompt, $previous, $node = .);
	    }
	    if (@type == "empty") {
		if ($value == "yes") {
		    <xsl:element name=$path>;
		}
	    } else {
		if (string-length($value) > 0) {
		    <xsl:element name=$path> $value;
		}
	    }
	}
    }
}

/*
 * Manage a choice of multiple children
 */
template wiz:manage-choice ($guide, $config, $last, $indent) {
    var $choice = {
	call wiz:prompt-choice($guide, $config, $last, $indent);
    }

    if ($choice) {
	var $new-guide = $guide/node()[@choice == $choice];
	var $this-last = $last/node()[name() == $choice];
	var $this-config = $config/node()[name() == $choice];

	if ($new-guide) {
	    var $new := {
		call wiz:build($config = $this-config, $last = $this-last,
			   $guide = $new-guide,
			   $indent = $indent _ $new-indent);
	    }
	    if ($new) {
		<xsl:element name=$new-guide/@path> {
		    copy-of $new;
		}
	    }
	} else {
	    expr wiz:message(">>> invalid option");
	    call wiz:manage-choice($guide, $config, $last, $indent);
	}
    }
}

/*
 * Prompt for a choice
 */
template wiz:prompt-choice ($guide, $config, $last, $indent) {
    var $prompt = $indent _ "Choose " _ $guide/@title;
    var $type1 = {
	for-each ($guide/node()) {
	    expr "," _ @choice;
	}
    }
    var $type = substring($type1, 2);
    var $default = $guide/@default;
    var $previous = {
	for-each ($guide/node()) {
	    var $path = @path;
	    var $that = $last/node()[name() == $path];
	    if ($that) {
		expr @choice;
	    }
	}
    }

    var $response = { call wiz:prompt($prompt, $default, $previous, $type); }
    if ($response && $response != "none") {
	expr $response;
    }
}

/*
 * Manage a list of instances
 * This is a bit tricky: we call this template recursively as
 * we build the config, so $list always has the config we
 * are building.
 */
template wiz:manage-list ($list, $guide, $indent, $op) {
    if (0) {
	expr wiz:message("manage-list: ", $op, " :: ", count($list));
	call dump($name = "list", $out = $list);
    }

    /*
     * If the caller passed in an operation, use that one.
     * Otherwise prompt for a suitable operation.
     */
    var $operation := {
	if (string-length($op)) {
	    expr $op;
	} else {
	    call wiz:prompt($prompt = $indent _ "Enter option",
			$default = "continue",
			$type = "add,delete,list,continue");
	}
    }

    if ($operation == "list") {
	call wiz:list-members($guide, $indent, $list = $list/node());
	call wiz:manage-list($list, $guide, $indent);
	
    } else if ($operation == "first-add") {
	var $add := { call wiz:make-list-member($list, $guide, $indent); }
	var $new := { call wiz:merge-lists($list, $add); }
	var $prompt = $indent _ "Add another " _ $guide/key/@title;
	var $more = {
	    call wiz:prompt($prompt, $default = "no", $type = "yes/no");
	}

	var $op = {
	    if ($more == "yes") {
		expr "first-add";
	    }
	}
	call wiz:manage-list($list = $new, $guide, $indent, $op);

    } else if ($operation == "add") {
	var $add := { call wiz:make-list-member($list, $guide, $indent); }
	var $new := { call wiz:merge-lists($list, $add); }
	call wiz:manage-list($list = $new, $guide, $indent);

    } else if ($operation == "delete") {
	var $name = { call wiz:prompt($prompt = $indent _ "Enter "
				  _ $guide/key/@title _ " to delete"); }

	var $old = $list/node()[name == $name];
	if ($old) {
	    var $rest := {
		copy-of $list/node()[name != $name];
	    }
	    expr wiz:message($indent, $guide/key/@title, " '", $name,
			    "' has been deleted");
	    call wiz:manage-list($list = $rest, $guide, $indent);
	} else {
	    expr wiz:message(">>> ", $guide/key/@title, " '", $name,
			    "' was not found");
	    call wiz:manage-list($list, $guide, $indent);
	}

    } else if ($operation == "continue" || $operation == "") {
	copy-of $list;
    } else {
	expr wiz:message(">>> invalid option");
	call wiz:manage-list($list, $guide, $indent);
    }
}

template wiz:list-members ($list, $indent, $guide) {
    expr wiz:message($indent, $guide/@title, ":");
    if (count($list) == 0) {
	expr wiz:message($indent, $new-indent, "(none)");
    } else {
	for-each ($list) {
	    var $line = $indent _ $new-indent _ $guide/key/@title _ ": ";
	    var $pad = substring($padding, 1,
				 $value-indent - string-length($line));
	    expr wiz:message($line, $pad, name);

	    call display-config($config = ., $guide,
				$indent = $indent _ $new-indent _ $new-indent);
	}
    }
}

template wiz:make-list-member ($list, $guide, $indent) {
    var $prompt = $indent _ "Enter " _ $guide/key/@title;
    var $name := { call wiz:prompt($prompt); }

    var $this = $list[name == $name];

    <xsl:element name=$guide/@path> {
	<name> $name;
	call wiz:build($config = /null, $last = $this,
		   $guide = ., $indent = $indent _ $new-indent);
    }
}

template wiz:merge-lists ($list, $add) {
    var $rest = $list/node()[name != $add/node()/name];
    var $new = $rest | $add/node();

    if (0) {
	call dump($name = "list", $out = $list);
	call dump($name = "add", $out = $add);
	for-each ($add/node()/name) {
	    expr wiz:message("add name ", .);
	}
	expr wiz:message("add: [", $add/node()/name, "] ", name($add/node()));
        call dump($name = "rest", $out = $rest);
	call dump($name = "new", $out = $new);
	for-each ($list/node()) {
	    expr wiz:message("[", name, "/", name(), "]");
	    if (name != $add/node()/name) {
	        expr wiz:message("   match");
	    }
	}
	expr wiz:message("count ", count($list), "/", count($rest));
    }

    copy-of $new;
}

var $padding = "                                                    ";

/*
 * Display the configuration in simple format
 */
template wiz:display-config ($guide, $config, $indent = $new-indent) {
    if (guide-builder) {
	var $new-guide := { call wiz:evaluate($expression = guide-builder); }

	call wiz:display-config-internal($guide = $new-guide, $config, $indent);

    } else {
	call wiz:display-config-internal($guide, $config, $indent);
    }
}

template display-config ($guide, $config, $indent = $new-indent) {
    call wiz:display-config($guide, $config, $indent);
}

template wiz:display-config-internal ($guide, $config, $indent = $new-indent) {
    if (false) {
	expr wiz:message("display: ", name($config), " ", count($config));
	call dump($name = "new", $out = $config);
    }

    for-each ($guide/node()) {
	var $path = @path;
	var $this = $config/node()[name() == $path];

	if (name() == "container") {
	    if ($this) {
		var $ind = {
		    if (@title) {
			expr wiz:message($indent, @title, ":");
			expr $indent _ $new-indent;
		    } else {
			expr $indent;
		    }
		}
		call display-config($config = $config/node()[name() == $path],
				    $guide = ., $indent = $ind);
	    }

	} else if (name() == "option") {
	    var $line = $indent _ @title _ ": ";
	    var $pad = substring($padding, 1,
				 $value-indent - string-length($line));
	    if ($this) {
		expr wiz:message($line, $pad, "yes");
		call display-config($config = $config/node()[name() == $path],
				    $guide = .,
				    $indent = $indent _ $new-indent);
	    } else {
		expr wiz:message($line, $pad, "no");
	    }

	} else if (name() == "list") {
	    if ($this) {
		call wiz:list-members($list = $this, $guide = ., $indent);
		/*
		expr wiz:message($indent, @title, ":");
		for-each ($this) {
		    var $line = $indent _ $new-indent _ key/@title _ ": ";
		    var $pad = substring($padding, 1,
		                         $value-indent - string-length($line));
		    expr wiz:message($line, $pad, name);

		    call display-config($guide = .,
                                $config = $config/node()[name() == $path],
				$indent = $indent _ $new-indent _ $new-indent);
		}
		*/
	    }

	} else if (name() == "choice") {
	    for-each (node()) {
		var $p2 = @path;
		var $that = $config/node()[name() == $p2];

		if ($that) {
		    var $line = $indent _ ../@title _ ": ";
		    var $pad = substring($padding, 1,
					 $value-indent - string-length($line));
		    expr wiz:message($line, $pad, @title);

		    call display-config($config = $that, $guide = .,
					$indent = $indent _ $new-indent);
		}
	    }

	} else if (name() == "leaf") {
	    if ($this) {
		var $line = $indent _ @title _ ": ";
		var $pad = substring($padding, 1,
				     $value-indent - string-length($line));
		if (@type == "secret") {
		    expr wiz:message($line, $pad, "********");
		} else if (@type == "empty") {
		    expr wiz:message($line, $pad, "yes");
		} else {
		    expr wiz:message($line, $pad, $this);
		}
	    }
	}
    }
}

template wiz:commit-config ($config, $dump) {
    if ($dump) {
	call dump($name = "configuration", $out = $config);
    }

    var $conn = jcs:open();

    if (not($conn)) {
	expr wiz:message("failed to get local connection");
    } else {
	call jcs:load-configuration($connection = $conn,
				    $configuration = $config);
    }
    expr jcs:close($conn);
}

template commit-config ($config, $dump) {
    call wiz:commit-config($config, $dump);
}

template wiz:abort ($message = "Aborted") {
    <xsl:message terminate="yes"> $message;
}

<func:function name="wiz:message"> {
    param $m1;
    param $m2;
    param $m3;
    param $m4;
    param $m5;

    var $value = $m1 _ $m2 _ $m3 _ $m4 _ $m5;

    if (function-available('jcs:output')) {
	expr jcs:output($value);

    } else {
	<xsl:message> $value;
    }
}

<func:function name="wiz:get-input"> {
    param $prompt;

    if (function-available('jcs:get-input')) {
	<func:result select="jcs:get-input($prompt)">;

    } else if (function-available('jcs:input')) {
	<func:result select="jcs:input($prompt)">;

    } else if (function-available('slax:get-input')) {
	<func:result select="slax:get-input($prompt)">;

    } else {
	expr wiz:message("wiz:get-input is not available");
    }
}

<func:function name="wiz:get-secret"> {
    param $prompt;

    if (function-available('jcs:get-secret')) {
	<func:result select="get-secret($prompt)">;

    } else if (function-available('jcs:getsecret')) {
	<func:result select="getsecret($prompt)">;

    } else if (function-available('slax:get-secret')) {
	<func:result select="get-secret($prompt)">;

    } else if (function-available('jcs:get-input')) {
	<func:result select="get-input($prompt)">;

    } else if (function-available('jcs:input')) {
	<func:result select="input($prompt)">;

    } else if (function-available('slax:get-input')) {
	<func:result select="get-input($prompt)">;

    } else {
	expr wiz:message("wiz:get-secret is not available");
    }
}

<func:function name="wiz:regex"> {
    param $pattern;
    param $input;

    if (function-available('jcs:regex')) {
	<func:result select="jcs:regex($pattern, $input)">;

    } else if (function-available('slax:regex')) {
	<func:result select="slax:regex($pattern, $input)">;
    } else {
	expr wiz:message("wiz:regex is not available");
    }
}

<func:function name="wiz:first-of"> {
    param $t1;
    param $t2;
    param $t3;
    param $t4;

    if (boolean($t1)) {
	<func:result select="$t1">;
    } else if (boolean($t2)) {
	<func:result select="$t2">;
    } else if (boolean($t3)) {
	<func:result select="$t3">;
    } else if (boolean($t4)) {
	<func:result select="$t4">;
    }

}
